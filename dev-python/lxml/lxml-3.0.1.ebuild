# Copyright 1999-2012 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: /var/cvsroot/gentoo-x86/dev-python/lxml/lxml-3.0.1.ebuild,v 1.3 2012/10/29 16:26:39 mgorny Exp $

EAPI=4
PYTHON_COMPAT=(python2_6 python2_7 python3_1 python3_2 python3_3)

inherit distutils-r1

MY_P="${PN}-${PV/_/}"

DESCRIPTION="A Pythonic binding for the libxml2 and libxslt libraries"
HOMEPAGE="http://codespeak.net/lxml/ http://pypi.python.org/pypi/lxml"
SRC_URI="http://codespeak.net/lxml/${MY_P}.tgz"
# SRC_URI="http://codespeak.net/lxml/dev/${MY_P}.tgz"

LICENSE="BSD ElementTree GPL-2 PSF-2"
SLOT="0"
KEYWORDS="~alpha ~amd64 ~arm ~hppa ~ia64 ~mips ~ppc ~ppc64 ~s390 ~sh ~sparc ~x86 ~amd64-fbsd ~x86-fbsd ~amd64-linux ~x86-linux ~ppc-macos ~x86-macos ~sparc-solaris ~x64-solaris ~x86-solaris"

# have to drop some keywords pending resolution of bug #438388
#KEYWORDS="~alpha ~amd64 ~arm ~hppa ~ia64 ~mips ~ppc ~ppc64 ~s390 ~sh ~sparc ~x86 ~amd64-fbsd ~x86-fbsd ~amd64-linux ~x86-linux ~ppc-macos ~x86-macos ~sparc-solaris ~x64-solaris ~x86-solaris"

IUSE="doc examples +threads"

RDEPEND=">=dev-libs/libxml2-2.7.2
	>=dev-libs/libxslt-1.1.15
	>=dev-python/beautifulsoup-3.0.8"
DEPEND="${RDEPEND}
	dev-python/setuptools"
# lxml tarball contains files pregenerated by Cython.

S="${WORKDIR}/${MY_P}"

src_prepare() {
	PATCHES=( "${FILESDIR}/${P}-skip-failing-test.patch" )
	distutils-r1_src_prepare
}

python_test() {
	# Tests broken with Python 3, generally due to Unicode.
	[[ ${EPYTHON} == python3.* ]] && return

	local module
	for module in "${BUILD_DIR}"/lib.*/lxml/*.so; do
		ln -fs "${module}" src/lxml/ || die
	done

	local exit_status="0" test
	local PYTHONPATH=src/ # needed for selftest*
	for test in test.py selftest.py selftest2.py; do
		einfo "Running ${test}"
		if ! "${PYTHON}" ${test}; then
			eerror "${test} failed with ${EPYTHON}"
			exit_status="1"
		fi
	done
	return "${exit_status}"
}

src_install() {
	distutils-r1_src_install

	if use doc; then
		dohtml -r doc/html/*
		dodoc *.txt
		docinto doc
		dodoc doc/*.txt
	fi

	if use examples; then
		docinto examples
		dodoc -r samples/*
		docompress -x /usr/share/doc/${PF}/examples
	fi
}
