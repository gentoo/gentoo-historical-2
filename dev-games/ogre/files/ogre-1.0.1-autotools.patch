Autotool code has a bunch of random mistakes, lets fix them.

- crosscompiling should look at $host, not at $build or $target
- use AC_MSG_RESULT() instead of doing a 'raw' echo
- AC_ARG_WITH / AC_ARG_ENABLE handles 3rd param incorrectly
- threading support uses boost but never links it in !?

--- acinclude.m4
+++ acinclude.m4
@@ -278,3 +278,3 @@
 AC_DEFUN([OGRE_SETUP_FOR_TARGET],
-[case $target in
+[case $host in
 *-*-cygwin* | *-*-mingw* | *-*-pw32*)
@@ -324,9 +324,9 @@
                              [Build the OpenEXR plugin]),
-              [build_exr=true],
-              [build_exr=false])
+              [build_exr=$enableval],
+              [build_exr=no])
 
-if test "x$build_exr" = "xtrue" ; then
-	PKG_CHECK_MODULES(OPENEXR, OpenEXR, [build_exr=true], [build_exr=false])
+if test "x$build_exr" = "xyes" ; then
+	PKG_CHECK_MODULES(OPENEXR, OpenEXR, [build_exr=yes], [build_exr=no])
 
-	if test "x$build_exr" = "xtrue" ; then
+	if test "x$build_exr" = "xyes" ; then
 	   	AC_CONFIG_FILES([ PlugIns/EXRCodec/Makefile \
@@ -341,3 +341,3 @@
 
-AM_CONDITIONAL(BUILD_EXRPLUGIN, test x$build_exr = xtrue)
+AM_CONDITIONAL(BUILD_EXRPLUGIN, test x$build_exr = xyes)
 
@@ -380,6 +380,6 @@
                              [Build the DirectX 9 Render System]),
-              [build_dx9=true],
-              [build_dx9=false])
+              [build_dx9=$enableval],
+              [build_dx9=no])
 
-AM_CONDITIONAL(BUILD_DX9RENDERSYSTEM, test x$build_dx9 = xtrue)
+AM_CONDITIONAL(BUILD_DX9RENDERSYSTEM, test x$build_dx9 = xyes)
 
@@ -391,8 +391,8 @@
                              [Don't use DevIL for image loading. This is not recommended unless you provide your own image loading codecs.]),
-              [build_il=false],
-              [build_il=true])
+              [build_il=$enableval],
+              [build_il=yes])
 
-AM_CONDITIONAL(USE_DEVIL, test x$build_il = xtrue)
+AM_CONDITIONAL(USE_DEVIL, test x$build_il = xyes)
 
-if [ test x$build_il = xtrue ]; then
+if test x$build_il = xyes ; then
 	AC_CHECK_LIB(IL, ilInit,,AC_MSG_ERROR([
@@ -418,9 +418,9 @@
 AC_MSG_CHECKING([whether -fPIC is needed])
-    case $build in
+    case $host in
         x86_64-*)
             CXXFLAGS="$CXXFLAGS -fPIC"
-            echo "yes"
+            AC_MSG_RESULT(yes)
         ;;
         *)
-            echo "no"
+            AC_MSG_RESULT(no)
         ;;
@@ -459,3 +459,3 @@
 			AC_DEFINE([OGRE_DOUBLE_PRECISION], [1], [Build with double precision])
-            echo "yes"
+            AC_MSG_RESULT(yes)
         ;;
@@ -463,3 +463,3 @@
 			AC_DEFINE([OGRE_DOUBLE_PRECISION], [0], [Build with double precision])
-            echo "no"
+            AC_MSG_RESULT(no)
         ;;
@@ -479,3 +479,4 @@
 			AC_DEFINE([OGRE_THREAD_SUPPORT], [1], [Build with thread support])
-            echo "yes"
+			OGRE_THREAD_LIBS="-lboost_thread-mt"
+			AC_MSG_RESULT(yes)
         ;;
@@ -483,5 +484,7 @@
 			AC_DEFINE([OGRE_THREAD_SUPPORT], [0], [Build with thread support])
-            echo "no"
+			OGRE_THREAD_LIBS=""
+			AC_MSG_RESULT(no)
         ;;
     esac
+AC_SUBST(OGRE_THREAD_LIBS)
 ])
--- OgreMain/src/Makefile.am.orig	2005-05-03 13:33:08.000000000 -0400
+++ OgreMain/src/Makefile.am	2005-05-03 13:37:12.000000000 -0400
@@ -156,3 +156,3 @@
 
-libOgreMain_la_LIBADD= $(STLPORT_LIBS) $(FT2_LIBS) $(ZZIPLIB_LIBS)
+libOgreMain_la_LIBADD= $(STLPORT_LIBS) $(FT2_LIBS) $(ZZIPLIB_LIBS) $(OGRE_THREAD_LIBS)
 libOgreMain_la_LDFLAGS = $(SHARED_FLAGS) -version-info @OGREMAIN_VERSION_INFO@
