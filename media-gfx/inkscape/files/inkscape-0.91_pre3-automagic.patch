Add configure options for aspell, gtkspell, and imagemagick.

--- inkscape-0.91pre3/configure.ac
+++ inkscape-0.91pre3/configure.ac
@@ -675,9 +675,16 @@
 dnl Check for ImageMagick Magick++ 
 dnl ******************************
 
-PKG_CHECK_MODULES(IMAGEMAGICK, ImageMagick++, magick_ok=yes, magick_ok=no)
-if test "x$magick_ok" = "xyes"; then
-      AC_DEFINE(WITH_IMAGE_MAGICK,1,[Image Magick++ support for bitmap effects])
+AC_ARG_ENABLE(magick,
+       AS_HELP_STRING([--enable-magick], [compile with Image Magick++ support for bitmap effects]),
+       enable_magick=$enableval,enable_magick=no)
+
+magick_ok="no"
+if test "x$enable_magick" = "xyes"; then
+	PKG_CHECK_MODULES(IMAGEMAGICK, ImageMagick++, magick_ok=yes, magick_ok=no)
+	if test "x$magick_ok" = "xyes"; then
+		AC_DEFINE(WITH_IMAGE_MAGICK,1,[Image Magick++ support for bitmap effects])
+	fi
 fi
 AM_CONDITIONAL(USE_IMAGE_MAGICK, test "x$magick_ok" = "xyes")
 
@@ -712,6 +719,10 @@
 	AC_DEFINE(WITH_GLIBMM_2_32,1,[Build with Glibmm 2.32 or higher])
 fi
 
+
+AC_ARG_WITH([gtkspell],
+	AS_HELP_STRING([--without-gtkspell], [disable gtk spelling widget]),[with_gtkspell=$withval], [with_gtkspell=yes])
+
 dnl *********************************
 dnl   Allow experimental GTK+3 build
 dnl *********************************
@@ -723,11 +734,13 @@
 if test "x$enable_gtk3" = "xyes"; then
 
 	ink_spell_pkg=
-	if pkg-config --exists gtkspell-3.0; then
-		ink_spell_pkg=gtkspell-3.0
-		AC_DEFINE(WITH_GTKSPELL, 1, [enable gtk spelling widget])
+	if test "x$with_gtkspell" = "xyes"; then
+		if pkg-config --exists gtkspell-3.0; then
+			ink_spell_pkg=gtkspell-3.0
+			AC_DEFINE(WITH_GTKSPELL, 1, [enable gtk spelling widget])
+		fi
 	fi
-
+	
 	PKG_CHECK_MODULES(GTK,
 			  gtkmm-3.0 >= 3.2
 			  gdkmm-3.0 >= 3.2
@@ -800,11 +813,13 @@
 else
 
 	ink_spell_pkg=
-	if pkg-config --exists gtkspell-2.0; then
-		ink_spell_pkg=gtkspell-2.0
-		AC_DEFINE(WITH_GTKSPELL, 1, [enable gtk spelling widget])
+	if test "x$with_gtkspell" = "xyes"; then
+		if pkg-config --exists gtkspell-2.0; then
+			ink_spell_pkg=gtkspell-2.0
+			AC_DEFINE(WITH_GTKSPELL, 1, [enable gtk spelling widget])
+		fi
 	fi
-
+	
 	PKG_CHECK_MODULES(GTK,
 			  gtk+-2.0  >= 2.24
 			  $ink_spell_pkg
@@ -928,12 +943,17 @@
 dnl **************************
 dnl Check for aspell 
 dnl ******************************
-AC_CHECK_LIB(aspell, new_aspell_config, [AC_CHECK_HEADER(aspell.h, aspell_ok=yes, aspell_ok=no)], aspell_ok=no, -lz -lm)
-if test "x$aspell_ok" = "xyes"; then
-	AC_DEFINE(HAVE_ASPELL, 1, [Use aspell for built-in spellchecker])
-  INKSCAPE_LIBS="$INKSCAPE_LIBS -laspell"
-else
-	AC_MSG_CHECKING([Aspell not found, spell checker will be disabled])
+AC_ARG_WITH([aspell],
+	AS_HELP_STRING([--without-aspell], [disable aspell spell checker]),[with_aspell=$withval], [with_aspell=yes])
+
+if test "x$with_aspell" = "xyes"; then
+	AC_CHECK_LIB(aspell, new_aspell_config, [AC_CHECK_HEADER(aspell.h, aspell_ok=yes, aspell_ok=no)], aspell_ok=no, -lz -lm)
+	if test "x$aspell_ok" = "xyes"; then
+		AC_DEFINE(HAVE_ASPELL, 1, [Use aspell for built-in spellchecker])
+		INKSCAPE_LIBS="$INKSCAPE_LIBS -laspell"
+	else
+		AC_MSG_CHECKING([Aspell not found, spell checker will be disabled])
+	fi
 fi
 
 dnl Check for bind_textdomain_codeset, including -lintl if GLib brings it in.
