# Copyright 1999-2008 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: /var/cvsroot/gentoo-x86/dev-dotnet/evolution-sharp/evolution-sharp-0.18.0.ebuild,v 1.3 2008/11/09 09:20:00 flameeyes Exp $

inherit mono gnome.org eutils autotools

DESCRIPTION="Mono bindings for Evolution"
HOMEPAGE="http://www.gnome.org/projects/beagle"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~amd64 ~ppc ~x86"
IUSE=""

RDEPEND=">=gnome-extra/evolution-data-server-1.8.1-r1
	>=dev-dotnet/gtk-sharp-2.4
	dev-lang/mono"
DEPEND="${RDEPEND}
	dev-util/pkgconfig"

src_unpack() {
	unpack ${A}
	cd "${S}"

	epatch "${FILESDIR}/${P}-respectlibdir.patch"

	# r188 broke TestCal compilation
	sed -i -e 's/TEST_TARGETS = $(TESTCAL_TARGET)/TEST_TARGETS = /g' \
		"${S}"/evolution/Makefile.am || die "sed TestCal failed."

	eautomake
}

src_install() {
	emake DESTDIR="${D}" install || die "emake install failed."
	dodoc AUTHORS ChangeLog MAINTAINERS NEWS README
}
