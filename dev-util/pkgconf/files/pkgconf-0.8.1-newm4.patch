commit 840ecc846d1a168b475a31bb927b78c3677d565a
Author: Dan Nicholson <dbn.lists@gmail.com>
Date:   Sat Jul 21 01:09:13 2012 -0500

    Add autoconf macros to help packagers install .pc files
    
    Some distros and users have unusual installation needs that don't
    conform to the standard $libdir/pkgconfig and $datadir/pkgconfig
    pathways. Help packagers support these users with the macros
    PKG_INSTALLDIR and PKG_NOARCH_INSTALLDIR. These are simple macros that
    provide the configure parameters --with-pkgconfigdir and
    --with-noarch-pkgconfigdir to allow users to override the standard
    locations.
    
    Freedesktop #48743

diff --git a/pkg.m4 b/pkg.m4
index 4748f99..0354609 100644
--- a/pkg.m4
+++ b/pkg.m4
@@ -158,6 +158,45 @@ else
 fi[]dnl
 ])# PKG_CHECK_MODULES
 
+# PKG_INSTALLDIR(DIRECTORY)
+# -------------------------
+# Substitutes the variable pkgconfigdir as the location where a module
+# should install pkg-config .pc files. By default the directory is
+# $libdir/pkgconfig, but the default can be changed by passing
+# DIRECTORY. The user can override through the --with-pkgconfigdir
+# parameter.
+AC_DEFUN([PKG_INSTALLDIR],
+[m4_pushdef([pkg_default], [m4_default([$1], ['${libdir}/pkgconfig'])])
+m4_pushdef([pkg_description],
+    [pkg-config installation directory @<:@]pkg_default[@:>@])
+AC_ARG_WITH([pkgconfigdir],
+    [AS_HELP_STRING([--with-pkgconfigdir], pkg_description)],,
+    [with_pkgconfigdir=]pkg_default)
+AC_SUBST([pkgconfigdir], [$with_pkgconfigdir])
+m4_popdef([pkg_default])
+m4_popdef([pkg_description])
+]) dnl PKG_INSTALLDIR
+
+
+# PKG_NOARCH_INSTALLDIR(DIRECTORY)
+# -------------------------
+# Substitutes the variable noarch_pkgconfigdir as the location where a
+# module should install arch-independent pkg-config .pc files. By
+# default the directory is $datadir/pkgconfig, but the default can be
+# changed by passing DIRECTORY. The user can override through the
+# --with-noarch-pkgconfigdir parameter.
+AC_DEFUN([PKG_NOARCH_INSTALLDIR],
+[m4_pushdef([pkg_default], [m4_default([$1], ['${datadir}/pkgconfig'])])
+m4_pushdef([pkg_description],
+    [pkg-config arch-independent installation directory @<:@]pkg_default[@:>@])
+AC_ARG_WITH([noarch-pkgconfigdir],
+    [AS_HELP_STRING([--with-noarch-pkgconfigdir], pkg_description)],,
+    [with_noarch_pkgconfigdir=]pkg_default)
+AC_SUBST([noarch_pkgconfigdir], [$with_noarch_pkgconfigdir])
+m4_popdef([pkg_default])
+m4_popdef([pkg_description])
+]) dnl PKG_NOARCH_INSTALLDIR
+
 # PKG_WITH_MODULES(VARIABLE-PREFIX, MODULES,
 #                  [ACTION-IF-FOUND],[ACTION-IF-NOT-FOUND],
 #                  [DESCRIPTION], [DEFAULT])
