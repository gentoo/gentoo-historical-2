diff -Nur kdeaddons-3.4.2.orig/kicker-applets/mediacontrol/configure.in.in kdeaddons-3.4.2/kicker-applets/mediacontrol/configure.in.in
--- kdeaddons-3.4.2.orig/kicker-applets/mediacontrol/configure.in.in	2005-07-20 18:42:57.000000000 +0200
+++ kdeaddons-3.4.2/kicker-applets/mediacontrol/configure.in.in	2005-07-26 12:54:48.000000000 +0200
@@ -32,6 +32,18 @@
   fi
 ])
 
-AC_CHECK_XMMS
+AC_ARG_WITH(xmms,
+  [AC_HELP_STRING(--with-xmms,
+    [enable XMMS control applet @<:@default=check@:>@])],
+  [], with_xmms=check)
+
+if test "x$with_xmms" != xno; then
+  AC_CHECK_XMMS
+
+  if test "x$with_xmms" != xcheck && test "x$ac_cv_have_xmms" != xyes; then
+    AC_MSG_ERROR([--with-xmms was given, but test for XMMS failed])
+  fi
+fi
+
 AC_SUBST(XMMS_LIBS)
 AC_SUBST(XMMS_INCLUDES)
diff -Nur kdeaddons-3.4.2.orig/noatun-plugins/configure.in.in kdeaddons-3.4.2/noatun-plugins/configure.in.in
--- kdeaddons-3.4.2.orig/noatun-plugins/configure.in.in	2005-07-20 18:42:41.000000000 +0200
+++ kdeaddons-3.4.2/noatun-plugins/configure.in.in	2005-07-26 12:54:18.000000000 +0200
@@ -173,14 +173,20 @@
 noatun_sdl=no
 dnl Check for SDL
 dnl require SDL 1.2 just because it's the new stable version, and it's what I have
-NOATUN_PATH_SDL(
-[1.2.0],
-[
-noatun_sdl=yes
-],
-[
-]
-)
+
+AC_ARG_WITH(sdl,
+  [AC_HELP_STRING(--with-sdl,
+    [enable SDL Noatun plugins @<:@default=check@:>@])],
+  [], with_sdl=check)
+
+if test "x$with_sdl" != xno; then
+  NOATUN_PATH_SDL([1.2.0], [noatun_sdl=yes], [])
+
+  if test "x$with_sdl" != xcheck && test "x$noatun_sdl" != xyes; then
+    AC_MSG_ERROR([--with-sdl was given, but test for SDL failed])
+  fi
+fi
+
 AM_CONDITIONAL(include_SDL, test "$noatun_sdl" = yes)
 
 AC_CHECK_HEADERS(endian.h)
diff -Nur kdeaddons-3.4.2.orig/noatun-plugins/oblique/configure.in.in kdeaddons-3.4.2/noatun-plugins/oblique/configure.in.in
--- kdeaddons-3.4.2.orig/noatun-plugins/oblique/configure.in.in	2005-07-20 18:42:35.000000000 +0200
+++ kdeaddons-3.4.2/noatun-plugins/oblique/configure.in.in	2005-07-26 12:53:34.000000000 +0200
@@ -1,19 +1,31 @@
+AC_ARG_WITH(berkeley-db,
+  [AC_HELP_STRING(--with-berkeley-db,
+    [enable support for Berkeley DB++ @<:@default=check@:>@])],
+  [], with_berkeley_db=check)
+
+AC_ARG_WITH(db-lib,
+  [AC_HELP_STRING([--with-db-lib=NAME],[name of the Berkeley DB++ library @<:@default=db_cxx@:>@])],
+    [ac_db_name="$withval"], [ac_db_name="db_cxx"])
 
-AC_MSG_CHECKING(for Berkeley DB++)
 berkeley_db=no
-AC_LANG_PUSH(C++)
-AC_TRY_COMPILE([
-		#include <db_cxx.h>
-	],[
-	],[
-		berkeley_db=yes
-	])
-AC_MSG_RESULT($berkeley_db)
-AC_DEFINE(BERKELEY_DB,1,[Define if you have Berkeley DB++ installed])
-BERKELEY_DB_LIBS="-ldb_cxx"
-AC_SUBST(BERKELEY_DB_LIBS)
-AC_LANG_POP(C++)
+if test "x$with_berkeley_db" != xno; then
+  berkeley_db=yes
 
-AM_CONDITIONAL(include_BERKELEY_DB, test "$berkeley_db" = yes)
+  KDE_CHECK_HEADER([db_cxx.h],
+    [:], [berkeley_db=no])
+
+  AC_CHECK_LIB([$ac_db_name], [main],
+    [:], [berkeley_db=no])
 
+  if test "x$berkeley_db" = xyes; then
+    AC_DEFINE(BERKELEY_DB, 1, [Define if you have Berkeley DB++ installed])
+    BERKELEY_DB_LIBS="-l$ac_db_name"
+    AC_SUBST(BERKELEY_DB_LIBS)
+  fi
 
+  if test "x$with_berkeley_db" != xcheck && test "x$berkeley_db" != xyes; then
+    AC_MSG_ERROR([--with-berkeley-db was given, but test for Berkeley DB++ failed])
+  fi
+fi
+
+AM_CONDITIONAL(include_BERKELEY_DB, test "$berkeley_db" = yes)
